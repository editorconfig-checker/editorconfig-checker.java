buildscript {
    repositories {
        jcenter()
		mavenCentral()
        // The next repo is only needed while using SNAPSHOT versions
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
    dependencies {
        classpath "net.saliman:gradle-cobertura-plugin:2.5.0"
    }
}

plugins {
	id 'net.saliman.cobertura' version '2.5.0'
	id "info.solidsoft.pitest" version '1.1.11'
	id 'com.github.kt3k.coveralls' version '2.8.1'
	id 'com.github.ben-manes.versions' version '0.15.0'
	id 'nebula.lint' version '7.8.0'
	id "com.github.johnrengelman.shadow" version "2.0.1"
	id "nebula.dependency-lock" version "4.9.4"
	id "nebula.gradle-git-scm" version "3.0.1"
}

pitest {
    timestampedReports = false
    outputFormats = ['XML']
}

cobertura.coverageFormats = ['html', 'xml'] // coveralls plugin depends on xml format report

group 'org.editorconfig'
version = '0.1.0'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'findbugs'
apply plugin: 'checkstyle'
apply plugin: 'pmd'

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'org.editorconfig.checker.Checker'

repositories {
    jcenter()
	mavenCentral()
}

gradleLint {
	// warn on these lints
	rules = ['dependency-parentheses']
	// fail the build on these lints
	criticalRules = ['unused-dependency']
}

dependencies {
    testRuntime 'org.slf4j:slf4j-log4j12:[1.0.0,2.0.0)'
    testRuntime 'log4j:log4j:[1.0.0,2.0.0)'
    compile 'org.ini4j:ini4j:[0.0.0,1.0.0)'
	// compile 'commons-cli:commons-cli:1.4'
	compile 'args4j:args4j:[2.0,3.0)'
	testCompile 'junit:junit:[4.0,5.0)'
}

build {
	dependsOn dependencyUpdates
}

jar {
    manifest {
        attributes 'Implementation-Version': version,
					'Specification-Version': version,
					'Main-Class': 'org.editorconfig.checker.Checker'
    }
}

// ignore unstable versions
dependencyUpdates.resolutionStrategy = {
	componentSelection { rules ->
		rules.all { ComponentSelection selection ->
			boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
				selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
			}
			if (rejected) {
				selection.reject('Release candidate')
			}
		}
	}
}

tasks.withType(FindBugs) {
	reports {
		xml.enabled false
		html.enabled true
	}
}

tasks.withType(Pmd) {
	reports {
		xml.enabled false
		html.enabled true
	}
}

checkstyle {
	configFile rootProject.file('config/checkstyle/checkstyle.xml')
	toolVersion = '7.8.2'
}

pmd {
	toolVersion = '5.7.0'
	ruleSetFiles = files('config/pmd/pmd-all-java.xml')
}

void prepare() {
    File f = new File(".git/hooks/pre-commit")
    if (!f.exists()) {
        println "Linking pre-commit git hook"
        java.nio.file.Files.createSymbolicLink(
            f.toPath(),
            new File("../../Build/GitHooks/pre-commit").toPath()
        )
    }
}
prepare()
